allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    project.logger.error(
                            "Warning: 'namespace' property for Flutter plugin '${project.name}' is not defined."
                                    + " Setting the 'namespace' property to  '${project.group}' as a fallback."
                                    + "\nWith AGP 8.+, the 'namespace' property must be explicitly defined in the module-level build script."
                                    + "\nIf a new version of '${project.name}' is not available, consider filing an issue against '${project.name}'"
                                    + " to add 'namespace' property on their module-level build script (otherwise try updating to the latest version)."
                    )
                    namespace project.group
                }
            }
        }

        Integer pluginCompileSdk = project.android.compileSdk
        if ((project.plugins.hasPlugin("com.android.application") || project.plugins.hasPlugin("com.android.library")) && pluginCompileSdk != null && pluginCompileSdk < 34) {
            project.logger.error(
                    "Warning: Overriding 'compileSdkVersion' version on Flutter plugin '${project.name}' from "
                            + pluginCompileSdk
                            + " to 36 (to meet AndroidX dependency requirements)."
                            + "\nSeveral AndroidX dependencies (fragment, lifecycle, core, etc.) now require compileSdk 34 or higher."
                            + "\nIf a new version of '${project.name}' is not available, consider filing an issue against '${project.name}'"
                            + " to increase their compileSdkVersion to at least 34 (preferably 36 for latest compatibility)."
            )
            project.android {
                compileSdkVersion 36
                buildToolsVersion "36.0.0"
            }
        }
    }

    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
